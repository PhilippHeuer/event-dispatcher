plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.jfrog.artifactory' version '4.8.1'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
}

group 'com.github.philippheuer.events4j'
description = 'A simple event dispatcher / consumer library.'
version = String.valueOf(System.getenv("CI_COMMIT_REF_NAME")).replace("v", "")

// All-Projects
allprojects {
    // Repositories
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/philippheuer/maven" }
    }

    // Artifact Info
    project.ext {
        groupId = 'com.github.philippheuer.events4j'
        artifactNamespace = 'philippheuer'
        artifactName = 'events4j'
        artifactVersion = String.valueOf(System.getenv("CI_COMMIT_REF_NAME")).replace("v", "") + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")
        artifactDescription = 'Events4J'
        websiteUrl = 'https://github.com/philippheuer/events4j'
        issueTrackerUrl = 'https://github.com/philippheuer/events4j/issues'
        vcsUrl = 'https://github.com/philippheuer/events4j.git'
    }
}

// Subprojects
subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.jfrog.artifactory'

    // Source Compatiblity
    sourceCompatibility = 1.8

    // Dependency Management for Subprojects
    dependencyManagement {
        imports {
            mavenBom "io.projectreactor:reactor-bom:Californium-RELEASE"
        }
        dependencies {
            // Logging
            dependency group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
            dependency group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

            // Testing
            dependency group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
            dependency group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'

            // Lombok
            dependency group: 'org.projectlombok', name: 'lombok', version: '1.18.2'
        }
    }

    // Common Dependencies
    plugins.withType(JavaPlugin) {
        dependencies {
            // Logging
            compile group: 'org.slf4j', name: 'slf4j-api'
            testCompile group: 'ch.qos.logback', name: 'logback-classic'

            // Test
            testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api'
            testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine'

            // Getter & Setter
            compileOnly group: 'org.projectlombok', name: 'lombok'
            testCompile group: 'org.projectlombok', name: 'lombok'
            annotationProcessor group: 'org.projectlombok', name: 'lombok'
        }
    }
}
