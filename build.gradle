plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
}

group 'com.github.philippheuer.events4j'
description = 'A simple event dispatcher / consumer library.'
version = String.valueOf(System.getenv("CI_COMMIT_REF_NAME")).replace("v", "")

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom "io.projectreactor:reactor-bom:Californium-RELEASE"
    }
}

dependencies {
    // SLF4J API
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'

    // Project Reactor
    compile group: 'io.projectreactor', name: 'reactor-core'

    // Micrometer
    compile group: 'io.micrometer', name: 'micrometer-core', version: '1.2.0'
    
    // Testing
    // - Reactor
    testCompile group: 'io.projectreactor', name: 'reactor-test'
    // - jUnit
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.1'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.1'
    // - Logging
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    
    // Getter & Setter
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
    testCompile group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
}

/**
 * Sources - build a jar with source files
 */
task sourcesJar(type: Jar, dependsOn: classes, description: 'Builds the sourcesJar.', group: 'build') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc, description: 'Builds the javadocJar.', group: 'build') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

/**
 * Artifacts
 */
artifacts {
    archives sourcesJar
    archives javadocJar
}

/**
 * Deployment
 */
publishing {
    publications {
        mainProject(MavenPublication) {
            from project.components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}

/**
 * Bintray Upload
 */
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mainProject']
    dryRun = false
    publish = true
    pkg {
        // jcenter repository namespace and name
        userOrg = String.valueOf(System.getenv('CI_PROJECT_NAMESPACE')).toLowerCase()
        repo = 'maven'
        name =  String.valueOf(project.name).toLowerCase()
        desc = project.description
        licenses = ['MIT']
        vcsUrl = System.getenv('CI_REPOSITORY_URL')
        labels = []
        publicDownloadNumbers = true
        version {
            name = String.valueOf(project.version).replace("v", "")
            vcsTag = project.version
            released  = new Date()
        }
    }
}
